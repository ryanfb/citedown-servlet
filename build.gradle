import org.apache.tools.ant.filters.*

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "edu.harvard.chs"
version = "0.0.2"

ext {
  citedownVersion = '1.2.1.0'
}

repositories {
    mavenCentral()
    maven {
        url "http://beta.hpcc.uh.edu/nexus/content/repositories/releases"
    }
    maven {
        url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/"
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
    }
}

apply plugin: 'fatjar'

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '1.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.3'
    compile group: 'commons-pool', name: 'commons-pool', version: '1.5.6'
    compile group: 'org.apache.jcs', name: 'jcs', version: '1.3'
    compile group: 'fast-md5', name: 'fast-md5', version: '2.5'
    compile group: 'edu.harvard.chs', name: 'citedown', version: citedownVersion
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '8.1.9.v20130131'
}

afterEvaluate {
  configure(allProcessResourcesTasks()) {
    filter(ReplaceTokens, tokens: ['citedownVersion': citedownVersion])
  }
}

def allProcessResourcesTasks() {
  sourceSets*.processResourcesTaskName.collect {
    tasks[it]
  }
}

publishing {
    publications {
        maven(MavenPublication) {
          artifact war
          artifact source: fatJar, classifier: 'jar', extension: 'jar' 
        }
    }
    repositories {
        maven {
            url nexusRepo
            credentials {
              username nexusUser
              password nexusPassword
            }
        }
    }
}


httpPort = 8988
stopPort = 9451
stopKey = 'foo'
